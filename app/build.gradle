apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'nu.studer.credentials'
apply from: "./../utils.gradle"

android {

    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    android {
        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }
    }

    flavorDimensions "pokemon"
    productFlavors {
        red {
            dimension "pokemon"
            versionName = "Pikachu"
            versionNameSuffix "-red"
            versionCode gitCommitCountBranchRed() // Version code: number of commits + 2000
        }
        blue {
            dimension "pokemon"
            versionName ="Mew"
            versionNameSuffix "-blue"
            versionCode gitCommitCountBranchBlue() // Version code: number of commits + 3000
        }
    }

    signingConfigs {
        // 1. Option with encrypted local credentials
        //    we need to run /keys/addCredentials.bat or /keys/addCredentials.sh in new machine
        // 2. Alternatively we could use system variables here (e.g. from CI chain)
        //    System.getenv("my-key-variable")
        debug {
            storeFile file("../keys/TestDebug.keystore")
            storePassword credentials.installAppKeyStore
            keyAlias credentials.installAppKeyStoreAlias
            keyPassword credentials.installAppKey
        }
        release {
            storeFile file("../keys/TestRelease.keystore")
            storePassword credentials.installAppKeyStore
            keyAlias credentials.installAppKeyStoreAlias
            keyPassword credentials.installAppKey
        }
    }

    defaultConfig {
        applicationId "pl.marcinkwasniak.install"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode gitCommitCountBranch() // Version code: number of commits + 1000
        versionName gitLastTag() // Version name from tag
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "long", "COMMITS", "${gitCommitCount()}"
        buildConfigField "long", "BUILD_TIMESTAMP", "${getTimestamp()}L"

        // Set application file name (both apk and aab bundle):
        setProperty("archivesBaseName", "install-test-app-" + versionName + "(" + versionCode + ")")
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.forEach { variant ->
        if (variant.buildType.name != "release") {
            variant.mergedFlavor.let {
                it.versionName = "sampingan-${defaultConfig.versionName}"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
